text = (text1 INTERSENTENCESPACE)* text1

text1 = supersentence ENDPUNCT+ / EMOTICON



# | ni'o cumki fa lo nu lo nu lo me'oi .parenthesis. co'e cu vasru lo me'oi .supersentence. poi ke'a pagbu lo sumvla pe'a je me'oi .clause. cu filri'a lo nu lo prenu poi ke'a na djuno le du'u pilno lo me'oi .parenthesis. lo nu me'oi .indicate. lo me'oi .grouping. kei je nai tu'a lo me'oi .parenthetical.
supersentence = supersentence1 / "(" supersentence1 ")"

supersentence1 = paragraphheader? introductory? (quantifier SPACE)* (compoundsentence / conditional)



conditional = conditional1 / "(" conditional1 ")"

conditional1 = conditional2 / conditional3

conditional2 = conditional4 SPACE logiconjunction SPACE conditional4

conditional3 = logiconjunction SPACE conditional3_5 COMMA SPACE THEN SPACE conditional3_5

conditional3_5 = conditional4 / supersentence

conditional4 = compoundsentence / "(" compoundsentence ")" / "(" supersentence ")"



compoundsentence = (noncompoundsentence SPACE SUCHTHAT SPACE)* compoundsentence1

compoundsentence1 = noncompoundsentence ((COMMA SPACE conjunction / ";") SPACE compoundsentence)?



quantifier = quantifier1 SPACE (variable / gnoun (SPACE variable)?) COMMA

quantifier1 = PUQUANTIFIER (SPACE "{" ("PQ" / "PUQUANTIFIER") "}")?



# ni'o cadga fa lo nu na me'oi .merge. la'oi la'oi .gnoun. la'oi .variable. kei ki'u le su'u djica lo nu gerna fi zoi glibau. A THING $A$ .glibau. jenai zoi glibau. A THING A CHAIR .glibau.
variable = VARIABLE (SPACE "{" "VAR" "IABLE"? "}")?



introductory = (namelist ":" SPACE)? introductory1? (conjunction SPACE)?

introductory1 = conjunction SPACE / (conjunction COMMA SPACE)? (introporw COMMA SPACE)+



noncompoundsentence = clause / interjection / LATEXCLAUSE



clause = clause1 / "(" clause1 ")"

clause1 = pluralclause / singularclause / intquestion



singularclause = singularclause1 / "(" singularclause1 ")"

# ni'o la .varik. cu stidi lo nu ci'au .o'i ko'a goi le su'u lakne fa lo nu su'o da poi glibau ke'a zo'u cfipu da fa lo nu lo me'oi .singularclause1. ja co'e cu na vasru lo me'oi .singularnoun. ja co'e... kei noi ke'a nibli lo nu na jufra me'oi .subject. lo jufra  .i ga je la .varik. na tolnei gi ku'i ga je la janbe je zo'e cu ci'au .o'i ko'a gi la .varik. cu djica lo nu frili fa lo nu jimpe fi lo te gerna be ko'a goi le glibau be la .varik... kei kei je ku'i cu toldji lo nu ko'a banzuka le ka ce'u sampu kei lo nu nandu fa lo nu skicu ja ciksi lo pluja fo ko'a bau ko'a
singularclause1 = (singularnoun SPACE)? singularpredicate



pluralclause = pluralclause1 / "(" pluralclause1 ")"

# ni'o srana fa le pinka be la'oi .singularclause1.
pluralclause1 = (pluralnoun SPACE)? pluralpredicate



# | ni'o jitfa fa le du'u la'oi .intquestion. mapti lo ro cmima be ko'a goi lo'i ro preti be fi la .varik. be'o poi gerna le glibau ke'a
# .i lo so'i cmima be ko'a cu smimlu lo so'i cmima be ko'e goi lo'i ro se retsku be la .varik. be'o poi gerna la .lojban. ke'a  .i lo so'i cmima be ko'e cu selneimau lo so'i cmima be ko'a la .varik.
#
# .i cumki fa lo nu cadga fa lo nu vimcu la'oi .intquestion. je zo'e
intquestion = intquestion1 (infinitive (SPACE gnoun)?)?

intquestion1 = shelpverb SPACE singularnoun / phelpverb SPACE pluralnoun



gnoun = gnoun1 / "(" gnoun1 ")"

gnoun1 = gnoun2 ((SPACE / COMMA SPACE (gnoun2 COMMA SPACE)+)? conjunction SPACE gnoun2)?

gnoun2 = gnoun2_5? (gnoun3 / "(" gnoun2 ")") / gnoun2_5 gnoun2

# | ni'o le prije pe la'o glibau. Looking Glass Studios .glibau. cu cusku zoi glibau. hack hack hack .glibau.
#
# .i sarcu fa lo nu pilno le se feglyzvi je nai la'oi .adjective. kei ki'u le su'u ga naja pilno la'oi .adjective. gi zunle cmacnrekursi  .i la .varik. na mutce le ka ce'u nelci le se feglyzvi  .i ku'i la .varik. cu djica lo nu cumki fa lo nu lo me'oi .gnoun. cu vasru lo me'oi .parenthesis.
#
# .i cumki fa lo nu la .varik. cu bixygau fi lo na feglyzvi
gnoun2_5 = (ADJECTIVE / SNOUN / PNOUN) SPACE

gnoun3 = (pluralnoun / singularnoun) (SPACE variable)? / variable



singularverb = singularverb1 / "(" singularverb1 ")"

singularverb1 = shelpverb SPACE infinitivephrase1 / singularverb2 ((SPACE / COMMA SPACE (singularverb2 COMMA SPACE)+) conjunction SPACE singularverb2)?

singularverb2 = singularverb3 / "(" singularverb3 ")"

singularverb3 = (singularverb4 / "(" singularverb4 ")") (SPACE prepositionalphrase)?

singularverb4 = (adverb SPACE)? sverb (SPACE adverb)? / adverb SPACE (singularverb3 / "(" singularverb3 ")")



pluralverb = pluralverb1 / "(" pluralverb1 ")"

pluralverb1 = phelpverb SPACE infinitivephrase1 / pluralverb2 (pluralverb2 (SPACE / COMMA SPACE (pluralverb2 COMMA SPACE)+)? conjunction SPACE)?

pluralverb2 = pluralverb3 / "(" pluralverb3 ")"

pluralverb3 = (pluralverb4 / "(" pluralverb4 ")") (SPACE prepositionalphrase)?

pluralverb4 = (adverb SPACE)? pverb (SPACE adverb)? / adverb SPACE (pluralverb3 / "(" pluralverb3 ")")



# ni'o tu'a le me'oi .conjunction. co'e cu filri'a lo nu gerna fi zo'e je zoi glibau. 1, 2, OR 3 IS THE SQUARE ROOT OF 4. .glibau. noi gendu'i zoi glibau. AN APPLE .glibau. fa zoi glibau. 1, 2, OR 3 .glibau. po ke'a
singularnoun = singularnoun1 ((SPACE / COMMA SPACE (singularnoun1 COMMA SPACE)+) conjunction SPACE singularnoun1)?

singularnoun1 = singularnoun2 / "(" singularnoun2 ")"

singularnoun2 = (singularnoun3 / "(" singularnoun3 ")") eggphrase?

singularnoun3 = (singularnoun4 / "(" singularnoun4 ")") (SPACE (singularrelativeclause / prepositionalphrase))?

singularnoun4 = (clausen / singularnoun5 / infinitivephrase) (SPACE (singularrelativeclause / prepositionalphrase))?

singularnoun5 = (article SPACE)? (adjective SPACE)? (singularnoun6 / "(" singularnoun6 ")")

singularnoun6 = adjective / squote / snoun / name / number



pluralnoun = pluralnoun1 ((SPACE / COMMA SPACE (pluralnoun1 COMMA SPACE)+) conjunction SPACE pluralnoun1)?

pluralnoun1 = pluralnoun2 / "(" pluralnoun2 ")"

pluralnoun2 = (pluralnoun3 / "(" pluralnoun3 ")") eggphrase?

pluralnoun3 = (pluralnoun4 / "(" pluralnoun4 ")") (SPACE (pluralrelativeclause / prepositionalphrase))?

pluralnoun4 = (number SPACE)? pluralnoun5 (SPACE (pluralrelativeclause / prepositionalphrase))?

pluralnoun5 = (article SPACE)? (adjective SPACE)? (pluralnoun6 / "(" pluralnoun6 ")")

pluralnoun6 = adjective / pquote / pnoun / namelist / number



pluralpredicate = pluralpredicate1 ((SPACE / COMMA SPACE (pluralpredicate1 COMMA SPACE)+) conjunction SPACE pluralpredicate1)?

pluralpredicate1 = (pluralpredicate2 / "(" pluralpredicate2 ")")

pluralpredicate2 = pluralverb (SPACE prepositionalphrase?) (SPACE gnoun)?



singularpredicate = singularpredicate1 ((SPACE / COMMA SPACE (singularpredicate COMMA SPACE)+) conjunction SPACE singularpredicate)?

singularpredicate1 = (singularpredicate2 / "(" singularpredicate2 ")")

singularpredicate2 = singularverb (SPACE prepositionalphrase)? (SPACE gnoun)?



pluralrelativeclause = pluralrelativeclause1 / COMMA SPACE pluralrelativeclause1 COMMA

pluralrelativeclause1 = relclauseopener SPACE pluralpredicate



singularrelativeclause = singularrelativeclause1 / COMMA SPACE singularrelativeclause1 COMMA

singularrelativeclause1 = relclauseopener SPACE singularpredicate



# | ni'o la .varik. cu zukyjdipei lo ka ce'u vimcu la'oi .gerund. je zo'e kei ki'u le su'u ro da poi ke'a me'oi .gerund. zo'u cumki fa lo nu basygau lo me'oi .clausen. da kei kei je le su'u la .varik. cu jinvi le du'u lo me'oi .clausen. cu zmadu la'oi .gerund. le ka frili fa lo nu tcidu ja setese jimpe ja co'e ce'u
gerund = gerund1 / "(" gerund1 ")"

gerund1 = (adverb SPACE)? gerundword prepositionalphrase? (SPACE gnoun)?



prepositionalphrase = prepositionalphrase1 / "(" prepositionalphrase1 ")"

prepositionalphrase1 = preposition SPACE gnoun (SPACE conjunction SPACE prepositionalphrase1)?



introporw = interjection / prepositionalphrase / gerund



clausen = clausen1 / "(" clausen1 ")"

clausen1 = clauseopener SPACE supersentence



clauseopener = CLAUSEOPENER (SPACE "{" ("CO" / "CLAUSEOPENER") "}")?



logiconjunction = LOGICONJUNCTION (SPACE "{" ("LOGICONJUNCTION" / "LC") "}")?



adjective = adjective1 / "(" adjective1 ")"

adjective1 = adjective2 ((SPACE / COMMA SPACE (adjective2 COMMA SPACE)+) conjunction SPACE adjective2)?

adjective2 = adjective3 / "(" adjective3 ")"

adjective3 = gnoun / (adverb SPACE)? (adjective4 / number)

adjective4 = ADJECTIVE (SPACE "{" "ADJ" "ECTIVE"? "}")?




relclauseopener = RELCLAUSEOPENER (SPACE "{" ("RCO" / ("REL" "ATIVE"?) "CLAUSEOPENER") "}")?



shelpverb = SHELPVERB (SPACE "{" "S"? "H"? "V" "ERB"? "}")?



phelpverb = PHELPVERB (SPACE "{" "P"? "H"? "V" "ERB"? "}")?



conjunction = CONJUNCTION (SPACE "{" "CON" "JUNCTION"? "}")?



# | ni'o la .varik. cu zukyjdipei lo ka ce'u me'oi .merge. la'oi .article. la'oi .adjective.
# .i ku'i lo me'oi .article. na prane le ka ce'u smimlu lo me'oi .adjective.  .i cadga fa lo nu lo me'oi .article. cu lidne lo me'oi .adjective.  .i lakne fa lo nu lo so'i se glibau cu tolnei zoi glibau. SHITTY THE CHAIR .glibau. ja cu sruma le du'u zo'oi .SHITTY. co'e ja cmene lo stizu
article = ARTICLE (SPACE "{ART" "ICLE"? "}")?



squote = SQUOTE (SPACE "{" "S"? "QUOTE}")?



snoun = SNOUN (SPACE "{" "S"? "N" "OUN"? "}")?



name = (NAME / "(" NAME ")") (SPACE "{NAME}")?



pquote = PQUOTE (SPACE "{" "P"? "QUOTE}")?



pnoun = PNOUN (SPACE "{" "P"? "N" "OUN"? "}")?



sverb = SVERB (SPACE "{" "S"? "V" "ERB"? "}")?



pverb = PVERB (SPACE "{" "P"? "V" "ERB"? "}")?



infinitive = (adverb SPACE)? infinitive1

infinitive1 = INFINITIVE (SPACE ("{" "INF" "INITIVE"? "}"))?



adverb = adverb1+ / "(" adverb1+ ")"

adverb1 = adverb2 (SPACE prepositionalphrase)?

adverb2 = adverb3 ((SPACE / COMMA SPACE (adverb3 COMMA SPACE)+) conjunction SPACE adverb3)?

adverb3 = adverb4 / "(" adverb ")"

adverb4 = ADVERB (SPACE "{" "ADV" "ERB"? "}")?



# ni'o cadga fa lo nu ma selma'o pe'a ru'e ko'a goi zo'oi .TO. po le glibau  .i ga je mutce le ka ce'u fegli la .varik. kei fa le su'u no da zo'u ko'a cmavo pe'a ru'e da gi ku'i la .varik. na birti lo du'u zabna fa lo su'u ma kau selma'o pe'a ru'e ko'a
# .i la .varik. cu milxe le ka ce'u toldji lo nu la .varik. cu pilno lo me'oi .infinitive.  .i krinu la'e di'u fa zo'e ja le su'u ga je la .varik. na birti lo du'u cadga fa lo nu ma selma'o pe'a ru'e ko'a gi fegli la .varik. fa lo nu pilno lo valsi poi na birti lo du'u ke'a cmavo ma kau
infinitivephrase = "TO" infinitivephrase1

infinitivephrase1 = SPACE infinitive (SPACE prepositionalphrase)? (SPACE gnoun)?



eggphrase = COMMA SPACE (eggphrase1 / eggphrase2) COMMA / SPACE "(" (eggphrase1 / eggphrase2) ")"

eggphrase1 = eggword COMMA SPACE gnoun

eggphrase2 = akaword SPACE gnoun

# | ni'o xu cadga fa lo nu vimcu
# .i to'e cafne fa lo nu la .varik. cu pilno zoi glibau. E.G. .glibau.
eggword = EG (SPACE "{EG}")?

akaword = AKA (SPACE "{AKA}") ?



preposition = PREPOSITION (SPACE "{PREP" "OSITION"? "}")?



gerundword = GERUNDWORD (SPACE "{" "GER" "UND"? "}")?



number = DIGIT+ (("." / "/") DIGIT+)? / latexpression



interjection = INTERJECTION



paragraphheader = gnoun "." INTERSENTENCESPACE



namelist = name ((SPACE / (COMMA SPACE (name COMMA SPACE)+)) conjunction name)?









DIGIT = "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"



LOGICONJUNCTION = "IF" / "IFF" / "IF" SPACE "AND" SPACE "ONLY" SPACE "IF" / "IF-AND-ONLY-IF" / "ONLY" SPACE "IF" / "ONLY-IF"



CONJUNCTION = ("BUT" / "AND") (SPACE "NOT" / "-" "NOT"?)? / "OR" / "JI"



# | ni'o zo'oi .PUQUANTIFIER. cmavlaka'i zoi glibau. POTENTIALLY UNIVERSAL QUALIFIER .glibau.
#
# ni'o cadga fa lo nu ma fa'u ma selma'o zo'oi .FOR. fa'u ke zo'oi .ALL. je zo'e  .i fegli la .varik. fa le su'u narcu'i fa lo nu cusku lo gerna tcita be zo'e ja zo'oi .FOR.
PUQUANTIFIER = "FOR" SPACE ("ALL" / "SOME" / "MANY")



SPACE = "..."? " "



ENDPUNCT = "?" / "." / "!"



THEN = "THEN"



SUCHTHAT = "SUCH" (SPACE / "-") "THAT" (SPACE "{SUCHTHAT}")?



INTERSENTENCESPACE = "  " / "\n"+



# | ni'o ga je ko'a goi zo'oi .EMOTICON. na prane le ka ce'u mapti gi ku'i la .varik. na birti lo du'u ma kau zmadu ko'a le ka ce'u mapti
# .i ranji fa le nu la .varik. cu djica curmi lo nu pinka
EMOTICON = (("%" / ":" / ";") "^" / ">") ")" / "(<" / "aw yeah bitches" / "hell ye boi" / "lol no" / "chicle"



AKA = "A.K.A."



EG = "I.E." / "E.G."



COMMA = ","



# | ni'o le su'o prenu cu xusra ko'a goi le du'u ko'e goi zo'oi .WHICH. pe zoi glibau. THE CHAIR WHICH IS KICKED UP (FINALLY IS) BUSTICATED. .glibau. mu'oi glibau. relative pronoun .glibau.  .i ku'i la .varik. cu mutce le ka ce'u toltu'i fi ko'a
#
# .i nibli le su'u ko'a na basyvla kei fa le su'u ga je ro da poi ke'a basyvla zo'u su'o da poi ke'a sumti zo'u da smudu'i de gi ko'e smudu'i lo no sumti
#
# .i ku'i la .varik. na jinvi le du'u su'o da poi ke'a cacklu pe'a ru'e je ke glibau selma'o cmene zo'u da xamgu basti zo'oi .RELCLAUSEOPENER. ja zo'e
RELCLAUSEOPENER = "WHICH" / "WHAT"



CLAUSEOPENER = "THAT"
